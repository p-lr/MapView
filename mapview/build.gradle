apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

def versionTag = "2.0.15"

androidExtensions {
    experimental = true
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 2
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }
}

dependencies {
    def coroutine_version = '1.3.9'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
}
repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.peterlaurence'
                artifactId = 'mapview'
                version = versionTag
            }
        }
    }

    bintray {
        try {
            user = System.getenv("bintrayUser")
            key = System.getenv("bintrayApikey")
            dryRun = System.getenv("dryRun").toBoolean()
        } catch (Exception e) {
        }

        publications = ['release']
        pkg {
            repo = 'maven'
            name = 'mapview'
            desc = 'Android Kotlin library to display tiled maps, with support of markers and paths'
            websiteUrl = 'https://github.com/peterLaurence/MapView'
            vcsUrl = 'https://github.com/peterLaurence/MapView.git'
            licenses = ["Apache-2.0"]
            publish = true
            publicDownloadNumbers = true
            version {
                name = versionTag
                vcsTag = versionTag
                released = new Date()
            }
        }
    }
}

